plugins {
  id "java-library"
  id "maven-publish"
  id "net.minecrell.licenser" version "0.4.1"
}

group "com.ichorpowered"
version "1.0.0-SNAPSHOT"
description "An error report generator"

sourceCompatibility = 1.8
targetCompatibility = 1.8

license {
  header = file("header.txt")
  newLine = false
  ext {
    name = "Indigo"
    organization = "IchorPowered"
    url = "http://ichorpowered.com"
  }

  include("**/*.java", "**/*.groovy", "**/*.kt")
}

repositories {
  mavenLocal()
  mavenCentral()
}

dependencies {
  api "org.checkerframework:checker-qual:3.16.0"
  api "com.google.code.gson:gson:2.8.0"
}

jar {
  manifest.attributes(
    "Automatic-Module-Name": "com.ichorpowered.indigo"
  )
}

java {
  withJavadocJar()
  withSourcesJar()
}

def ichorpoweredUsername = System.getenv("ICHORPOWERED_USERNAME")
def ichorpoweredPassword = System.getenv("ICHORPOWERED_PASSWORD")

tasks.<PublishToMavenRepository>withType(PublishToMavenRepository.class).configureEach {
  onlyIf {
    (repository == publishing.repositories["GitHubPackages"]
      || (ichorpoweredUsername != null && ichorpoweredPassword != null))
  }
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java

      pom {
        name = project.name
        url = "https://github.com/ichorpowered/indigo/"
        description = project.description

        issueManagement {
          system = "GitHub Issues"
          url = "https://github.com/ichorpowered/indigo/issues"
        }

        licenses {
          license {
            name = "MIT"
            url = "https://opensource.org/licenses/MIT"
          }
        }

        scm {
          connection = "scm:git@github.com:ichorpowered/indigo.git"
          developerConnection = "scm:git@github.com:ichorpowered/indigo.git"
          url = "https://github.com/ichorpowered/indigo/"
        }
      }
    }
  }
  repositories {
    maven {
      name = "GitHubPackages"
      url = "https://maven.pkg.github.com/ichorpowered/indigo"
      credentials {
        username = project.findProperty("gpr.user") as String ?: System.getenv("GITHUB_USERNAME")
        password = project.findProperty("gpr.key") as String ?: System.getenv("GITHUB_TOKEN")
      }
    }
    maven {
      name = "IchorpoweredRepo"
      url = "https://repo.ichorpowered.com/repository/maven-ichorpowered"
      credentials {
        username = ichorpoweredUsername
        password = ichorpoweredPassword
      }
    }
  }
}
